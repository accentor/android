plugins {
    id('com.android.application')
    id('kotlin-android')
    id('kotlin-kapt')
    id('dagger.hilt.android.plugin')
    id('org.jlleitschuh.gradle.ktlint')
}

android {
    compileSdkVersion 33
    buildToolsVersion "33.0.0"
    defaultConfig {
        applicationId "me.vanpetegem.accentor"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode 38
        versionName "0.13.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    buildFeatures {
        compose true
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility "1.8"
        sourceCompatibility "1.8"
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
            "-opt-in=androidx.compose.material.ExperimentalMaterialApi",
            "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi",
            "-opt-in=coil.annotation.ExperimentalCoilApi"
        ]
    }
    lintOptions {
        quiet true
        checkAllWarnings true
        disable 'OldTargetApi'
        disable 'SyntheticAccessor'
        disable 'VectorPath'
        disable 'InvalidPackage'
        disable 'GradleDependency'
        disable 'ObsoleteLintCustomCheck'
        ignoreWarnings false
        warningsAsErrors true
        textReport true
        textOutput "stdout"
        explainIssues !project.hasProperty("isCI")
    }
}

tasks.lint.dependsOn(ktlintCheck)

ktlint {
    android = true
}

dependencies {
    // Jetbrains Stdlibs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:$kotlinx_version"

    // AndroidX core
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.core:core-ktx:1.8.0'

    // AndroidX navigation
    implementation "androidx.navigation:navigation-compose:2.5.1"

    // AndroidX Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // AndroidX Lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // AndroidX Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.activity:activity-compose:1.5.1"

    // AndroidX Compose SwipeRefresh
    implementation "com.google.accompanist:accompanist-swiperefresh:0.25.1"

    // AndroidX Hilt (dependency injection)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Coil (image loading)
    implementation("io.coil-kt:coil:$coil_version")
    implementation("io.coil-kt:coil-compose:$coil_version")
    implementation("io.coil-kt:coil-gif:$coil_version")

    // AndroidX media3
    implementation "androidx.media:media:1.6.0" // For notification mediastyle
    implementation "androidx.media3:media3-common:$media3_version"
    implementation "androidx.media3:media3-database:$media3_version"
    implementation "androidx.media3:media3-datasource:$media3_version"
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-session:$media3_version"

    // Fuel
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel_version"
    implementation "com.google.code.gson:gson:2.9.1"

    // Material
    implementation 'com.google.android.material:material:1.6.1'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
