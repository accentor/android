plugins {
    id('com.android.application')
    id('com.google.devtools.ksp')
    id('kotlin-android')
    id('dagger.hilt.android.plugin')
    id('org.jetbrains.kotlin.plugin.compose')
    id('org.jlleitschuh.gradle.ktlint')
}

android {
    namespace 'me.vanpetegem.accentor'
    compileSdkVersion 36
    buildToolsVersion "36.0.0"
    defaultConfig {
        applicationId "me.vanpetegem.accentor"
        minSdkVersion 26
        targetSdkVersion 36
        versionCode 45
        versionName "0.18.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    buildFeatures {
        buildConfig true
        compose true
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility "17"
        sourceCompatibility "17"
    }
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += [
            "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api",
            "-opt-in=androidx.compose.ui.ExperimentalComposeUiApi",
            "-opt-in=coil.annotation.ExperimentalCoilApi"
        ]
    }
    lintOptions {
        quiet true
        checkAllWarnings true
        disable 'AndroidGradlePluginVersion'
        disable 'GradleDependency'
        disable 'MemberExtensionConflict'
        disable 'NewerVersionAvailable'
        disable 'ObsoleteLintCustomCheck'
        disable 'OldTargetApi'
        disable 'SyntheticAccessor'
        disable 'UnusedAttribute'
        disable 'VectorPath'
        ignoreWarnings false
        warningsAsErrors true
        lintConfig = file("lint.xml")
        textReport true
        textOutput "stdout"
        explainIssues !project.hasProperty("isCI")
    }
}

tasks.lint.dependsOn(ktlintCheck)

ktlint {
    android = true
}

dependencies {
    // Jetbrains Stdlibs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:$kotlinx_version"

    // AndroidX core
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.17.0'

    // AndroidX navigation
    implementation "androidx.navigation:navigation-compose:2.9.5"

    // AndroidX Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // AndroidX Lifecycle
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"

    // AndroidX Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material3:material3:1.3.2"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.activity:activity-compose:1.11.0"

    // AndroidX Hilt (dependency injection)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.3.0"

    // Coil (image loading)
    implementation("io.coil-kt:coil:$coil_version")
    implementation("io.coil-kt:coil-compose:$coil_version")
    implementation("io.coil-kt:coil-gif:$coil_version")

    // AndroidX media3
    implementation "androidx.media:media:1.7.1" // For notification mediastyle
    implementation "androidx.media3:media3-common:$media3_version"
    implementation "androidx.media3:media3-database:$media3_version"
    implementation "androidx.media3:media3-datasource:$media3_version"
    implementation "androidx.media3:media3-datasource-okhttp:$media3_version"
    implementation "androidx.media3:media3-exoplayer:$media3_version"
    implementation "androidx.media3:media3-session:$media3_version"

    // Fuel
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel_version"
    implementation "com.google.code.gson:gson:2.13.2"

    // Material
    implementation 'com.google.android.material:material:1.13.0'

    // Error reports
    implementation("ch.acra:acra-mail:$acra_version")
    implementation("ch.acra:acra-dialog:$acra_version")

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.test.ext:junit:1.3.0"
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.2.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}
